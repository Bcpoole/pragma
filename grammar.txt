program : optStatementList

funcDef  : ID OPAREN optParamList CPAREN block

optParamList  : *empty*
              | paramList

paramList : expr1
          | expr1 COMMA paramList
            
optArgList  : *empty*
            | argList

argList : expr1
        | expr1 COMMA argList

expr1   : expr1 op1 expr2 | expr2
op1     : PLUS_TO | MINUS_TO | TIMES_TO | DIVIDES_TO | MOD_TO | EXPN_TO | ASSIGN
expr2   : expr2 op2 expr3 | expr3
op2     : OR
expr3   : expr3 op3 expr4 | expr4
op3     : AND
expr4   : expr4 op4 expr5 | expr5
op4     : EQUAL | NOT_EQUAL
expr5   : expr5 op5 expr6 | expr6
op5     : LESSTHAN | LESSTHAN_EQUALTO | GREATERTHAN | GREATERTHAN_EQUALTO
expr6   : expr6 op6 expr7 | expr7
op6     : PLUS | MINUS
expr7   : expr7 op7 expr8 | expr8
op7     : TIMES | DIVIDES | MOD
expr8   : expr8 op8 expr9 | expr9
op8     : EXPN
expr9   : expr9 op9 expr10 | expr10
op9     : INCREMENT | DECREMENT
expr10   : expr10 op10 primary | primary
op10     : DOT

primary : ID
        | STRING
        | INTEGER
        | lambda
        | funcDef
        | MINUS INTEGER
        | ID OPAREN optArgList CPAREN
        | optArray
        | dictionary

block : OBRACE optStatementList CBRACE

optStatementList  : *empty*
                  | statementList

statementList : statement
              | statement statementList

statement : varDef SEMI
          | whileLoop
          | expr1 SEMI
          | ifStatement
          | RETURN expr1 SEMI
          | funcDef

whileLoop : WHILE OPAREN expr1 CPAREN block

ifStatement : IF OPAREN expr1 CPAREN block optElif

optElif : optElse
        | ELIF OPAREN expr1 CPAREN block optElif

optElse : *empty*
        | ELSE block

varDef  : ID optAssign

optAssign : *empty*
          | ASSIGN expr1

optArray : OBRACKET CBRACKET
        | OBRACKET array CBRACKET

array : primary
      | primary COMMA array

lambda : OPAREN optArgList CPAREN LAMBDA_DEF block

dictionary : OBRACE optDictList CBRACE

optDictList : *empty*
            | dictList

dictList : STRING COLON primary
         | STRING COLON primary COMMA dictList
